plugins {
    id 'java'
    id 'application'
//    pulls the correct platform dependent native libraries onto the classpath path
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.beryx.jlink' version '2.24.1'
}

group = 'io.github.vincemann'
version = '1.1.0'


application {
    mainModule = 'io.github.vincemann.subtitlebuddy'
    mainClass = 'io.github.vincemann.subtitlebuddy.Main'
}

ext {
    // Use the provided targetPlatform property if available, otherwise use the detected OS
    targetPlatform = project.hasProperty('targetPlatform') ? project.targetPlatform : evalCurrentOs()
    releaseDir = "${projectDir}/build/releases"
}

static def evalCurrentOs() {
    def os = org.gradle.internal.os.OperatingSystem.current()
    if (os.isWindows()) {
        return "win"
    } else if (os.isLinux()) {
        return "linux"
    } else if (os.isMacOsX()) {
        return "mac"
    }
    throw new GradleException("cant eval current os")
}


javafx {
    version = "17"
    modules = ['javafx.controls', 'javafx.fxml']
    platform = targetPlatform
}


test {
    // Set this to true to capture printed output during tests
    testLogging.showStandardStreams = true

    // Configure which events get logged
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        // To show detailed exception messages and stack traces in the console
        exceptionFormat "full"
    }
    doFirst {
        // Initialize the list with common JVM arguments
        List<String> args = [
                '--add-modules', javafx.modules.join(","),
                '--add-reads', 'org.apache.commons.configuration2=ALL-UNNAMED',
                '--add-exports', 'javafx.graphics/com.sun.javafx.application=org.testfx',
                '--add-opens', 'javafx.graphics/com.sun.javafx.application=org.testfx'
        ]

        // Conditionally add module-path based on the target platform
        if (project.targetPlatform == 'win') {
            println("adding win javafx modules to module path")
            args += ['--module-path', "${projectDir}/lib/javafx/win/jmods"]
        }
        // doesnt seem to be needed
//        else if (project.targetPlatform == 'mac') {
//            println("adding osx javafx modules to module path")
//            args += ['--module-path', "${projectDir}/lib/javafx/osx/jmods"]
//        }

        // Assign the constructed argument list to jvmArgs
        jvmArgs = args
    }
}

// copies native jnativehook libraries into the image, which can be picked up by JLinkJNativeLibraryLocator
tasks.register("copyNativeLibs", Copy) {
    mustRunAfter "jlink"
    from "${projectDir}/lib/jnativehook"
    into "${projectDir}/build/image/appLauncher-${targetPlatform}/lib/jnativehook"
    doLast {
        println "Native libraries copied to ${destinationDir}"
    }
}

tasks.named('jlink').configure {
    finalizedBy 'copyNativeLibs'
}

// builds platform specific jlink images in build/images
// NOTE: for mac this only works, when building on mac for whatever reason - otherwise it will segfault
jlink {
    options = [
            '--strip-debug',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages'
    ]


    switch (targetPlatform) {
        case 'linux':
            targetPlatform("linux") {
                jdkHome = jdkDownload('https://github.com/AdoptOpenJDK/openjdk17-binaries/releases/download/jdk-2021-05-07-13-31/OpenJDK-jdk_x64_linux_hotspot_2021-05-06-23-30.tar.gz')
            }
            break
        case 'win':
            targetPlatform("win") {
                jdkHome = jdkDownload('https://github.com/AdoptOpenJDK/openjdk17-binaries/releases/download/jdk-2021-05-07-13-31/OpenJDK-jdk_x64_windows_hotspot_2021-05-06-23-30.zip')
                addExtraModulePath("${projectDir}/lib/javafx/win/jmods")
            }
            break
        case 'mac':
            targetPlatform("mac") {
                jdkHome = jdkDownload('https://github.com/AdoptOpenJDK/openjdk17-binaries/releases/download/jdk-2021-05-07-13-31/OpenJDK-jdk_x64_mac_hotspot_2021-05-06-23-30.tar.gz')
                addExtraModulePath("${projectDir}/lib/javafx/osx/jmods")
            }
            break
        default:
            throw new GradleException("need to specify target platform")
    }

    launcher {
        name = 'appLauncher'
        mainClass = application.mainClass.get()
        forceMerge('log4j-api')
        // fixes : package org.apache.commons.logging is declared in module org.apache.commons.logging, but module io.github.vincemann.merged.module does not read it
        forceMerge('commons-logging')
        // make sure symlinks work when installed via homebrew and more
        unixScriptTemplate = new File("${projectDir}/releases/linux/appLauncher")
        jvmArgs = [
                // tells application to use the jlink lib locator for jnativehook
                '-Djlink=true',
                '--add-reads', 'org.apache.commons.configuration2=ALL-UNNAMED',
                '--add-reads', 'io.github.vincemann.merged.module=org.apache.commons.logging',
                '--add-reads', 'org.apache.commons.configuration2=io.github.vincemann.merged.module',
                "--add-exports", "org.apache.commons.logging/org.apache.commons.logging.impl=org.apache.commons.configuration2"
        ]
    }
}

// creates tarball for jlink image and stores in build dir
// execute with -PtargetPlatform=[linux|win|mac]
// this can then be shipped via homebrew
// NOTE: for mac this only works, when building on mac for whatever reason - otherwise it will segfault
tasks.register('tarJlink', Exec) {
    dependsOn 'jlink'

    // Set the working directory and define the tarball path outside the doFirst
    workingDir "${projectDir}/build/image/appLauncher-${targetPlatform}"
    def tarballPath = "${releaseDir}/subtitle-buddy-${version}-${targetPlatform}x64.tar.gz"

    doFirst {
        // Ensure any previous tarball is deleted before creating a new one
        delete tarballPath
        mkdir releaseDir
    }

    // Define the command line to execute within the main configuration block
    commandLine 'tar', '-czvf', tarballPath, '.', '--transform=s,^./,image/,'

    // Capture and print the output
    standardOutput = new ByteArrayOutputStream()
    errorOutput = new ByteArrayOutputStream()
    doLast {
        println("STDOUT: ${standardOutput.toString()}")
        println("STDERR: ${errorOutput.toString()}")
    }
}


// uses app image tool generating binary, which must be placed into: releases/linux/appimagetool.AppImage
// execute with -PtargetPlatform=linux
tasks.register('buildLinuxAppImage') {
    dependsOn('jlink')
    doFirst {
        mkdir releaseDir
    }
    doLast {
        if (targetPlatform != "linux") {
            throw new GradleException("Need to specify linux target platform or run on linux")
        }
        // Define version and paths
        def baseDir = "${projectDir}/releases/linux/x64"
        def imageName = "subtitle-buddy-${version}-linuxx64.AppImage"
        def linuxImagePath = "${releaseDir}/${imageName}"
        def appImagePath = "${baseDir}/SubtitleBuddy.AppDir/usr/image"
        def jlinkImagePath = "${projectDir}/build/image/appLauncher-linux"  // Path to the jlink runtime image

        // Prepare AppDir for AppImage
        println "Preparing AppDir with jlink image..."
        delete appImagePath  // Clear the previous contents
        mkdir appImagePath
        copy {
            from "$jlinkImagePath"  // Copy runtime image created by jlink
            into appImagePath
        }
        println "jlink runtime image copied"

        // Build AppImage
        println "Building AppImage..."
        exec {
            environment "ARCH", "x86_64"
            commandLine 'bash', '-c', "cd ${baseDir} && ../appimagetool.AppImage SubtitleBuddy.AppDir $linuxImagePath"
        }
        println "AppImage created at $linuxImagePath"
        println "Done with linux x64 AppImage build"
    }
}

// uses a docker container with image name 'amake/innosetup' that uses wine for building windows installer from linux host machines
// execute with -PtargetPlatform=win
tasks.register('buildWindowsInstaller') {
    dependsOn('jlink')
    if (targetPlatform != "win") {
        throw new GradleException("Need to specify win target platform")
    }
    def os = evalCurrentOs()
    if (os == "win") {
        throw new GradleException("This task is meant for building windows installer on a linux host machine. " +
                "For building on windows use 'gradlew.bat clean winJpackage -PtargetPlatform=win'")
    }
    doFirst {
        mkdir releaseDir
    }
    doLast {
        def baseDir = "${projectDir}/releases/windows/x64"
        def installerBaseDir = "${baseDir}/inno"
        def outputExeName = "subtitle-buddy-installer.exe"
        def installerName = "subtitle-buddy-${version}-windowsx64-installer.exe"
        def outputExePath = "${installerBaseDir}/output/${outputExeName}"
        def appImagePath = "${installerBaseDir}/image"
        def jlinkImagePath = "${projectDir}/build/image/appLauncher-win" // Path to the jlink runtime image

        // Ensure target directories exist
        file(installerBaseDir).mkdirs()

        // Deploy the jlink image to the installer directory
        delete "$appImagePath"
        mkdir "$appImagePath"
        copy {
            from "$jlinkImagePath" // Copy runtime image created by jlink
            into "$appImagePath"
        }
        println "Deployed jlink runtime image to installer directory"

        // Building the installer using Inno Setup within a Docker container
        println "Building installer"
        exec {
            commandLine 'bash', '-c', "cd ${installerBaseDir} && docker run --rm -v \"\$PWD:/work\" amake/innosetup subtitle-buddy.iss"
        }

        def installerPath = "${releaseDir}/${installerName}"
        delete installerPath
        copy {
            from "${installerBaseDir}/output/${outputExeName}"
            into "${releaseDir}"
            rename { String fileName ->
                installerName
            }
        }
        delete outputExePath
        println "Installer created at ${installerPath}"
    }
}


// uses genisoimage linux cli tool, need to install it first
// execute with -PtargetPlatform=mac
tasks.register('buildMacDmg') {
    dependsOn 'jlink' // Ensure this task runs after the jlink image is created
    if (targetPlatform != "mac") {
        throw new GradleException("Need to specify mac target platform or run on mac")
    }
    doLast {
        def baseDir = "${projedctDir}/releases/macos/x64"
        def imagePath = "${baseDir}/SubtitleBuddy.app/Contents/Java/image"
        def dmgName = "subtitle-buddy-${version}-macx64.dmg"
        def dmgOutputPath = "${baseDir}/$dmgName"
        def jlinkImagePath = "${projectDir}/build/image/appLauncher-mac" // Path to the jlink runtime image

        println "Version: $version"
        println "jlink Image Path: $jlinkImagePath"

        // Ensure the application directory exists
        file(imagePath).mkdirs()

        // Copy the jlink image to the application directory
        println "Deploying jlink runtime image to application directory..."
        delete imagePath
        mkdir imagePath
        copy {
            from jlinkImagePath
            into imagePath
        }
        println "jlink runtime image copied"

        // Build DMG
        println "Building DMG image..."
        exec {
            commandLine 'bash', '-c', "cd ${baseDir} && genisoimage -D -V 'Subtitle Buddy' -no-pad -r -apple -o ${dmgOutputPath} SubtitleBuddy.app/"
        }
        println "DMG created at $dmgOutputPath"
        println "Done with macOS x64"
    }
}


java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
    modularity.inferModulePath.set(true)
}

// builds fat jar containing javafx moules and all other deps
// in windows and macos jmods of javafx need to be shipped and declared in modulepath anyways
// after building, jar can be launched like that (when jdk17 is installed):
// linux:   java --add-modules javafx.controls,javafx.fxml -jar /path/to/myjar.jar
// osx:     java --module-path lib/javafx/osx/sdk/lib --add-modules javafx.controls,javafx.fxml -jar /path/to/myjar.jar
// win:     java --module-path lib/javafx/win/jmods --add-modules javafx.controls,javafx.fxml -jar /path/to/myjar.jar
// ( note that osx seems to need sdk jars and not jmods )
shadowJar {
    archiveClassifier.set('')
    archiveFileName = "subtitle-buddy-${version}-${targetPlatform}.jar"
    manifest {
        attributes 'Main-Class': application.getMainClass().get()
    }
    mergeServiceFiles {}
    // this removes needed classes for slf4j, making dynamic class loading of slf4j impl fail
    // also the default jlink native lib loader probably wont work as well
    // todo configure minimize with exceptions
//    minimize()  // Optional: minimize the jar to remove unused classes
}

repositories {
    mavenCentral()
}


// in windows this command in cli is needed java --module-path /path/to/javafx-sdk/lib --add-modules javafx.controls,javafx.fxml -jar /path/to/myjar.jar
run {
    doFirst {
        // Initialize the list with common JVM arguments
        List<String> args = [
                // modules are not part of sdk anymore - need to add them manually to module graph
                "--add-modules", javafx.modules.join(","),
                '--add-reads', 'org.apache.commons.configuration2=ALL-UNNAMED',
//                "--add-exports", "org.apache.commons.logging/org.apache.commons.logging.impl=org.apache.commons.configuration2"
        ]

        // Conditionally add module-path based on the target platform
        if (project.targetPlatform == 'win') {
            println("adding win javafx modules to module path")
            args += ['--module-path', "${projectDir}/lib/javafx/win/jmods"]
        }
        // for whatever reason I cant get this to work on mac - always fails with FindException Module javafx.controls not found
        // even when specifying path to javafx jmods or javafx sdk/lib
        else if (project.targetPlatform == 'mac') {
            println("adding osx javafx modules to module path")
            args += ['--module-path', "${projectDir}/lib/javafx/osx/jmods"]
        }

        // Assign the constructed argument list to jvmArgs
        jvmArgs = args
    }
}

dependencies {

    // capture mouse and keyboard events
    implementation 'com.github.kwhat:jnativehook:2.2.2'

    // di
    implementation('com.google.inject:guice:7.0.0')
    implementation('com.google.inject.extensions:guice-assistedinject:7.0.0')
    implementation('com.google.inject.extensions:guice-multibindings:4.2.3')
    implementation('commons-io:commons-io:2.16.1')

    // logging
    // using a version here that works with jlink plugin
    implementation 'org.apache.logging.log4j:log4j-api:2.11.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.11.1'
    // route apache commons logging to log4j2 (commons configuration depends on using commons logging)
    runtimeOnly 'org.apache.logging.log4j:log4j-jcl:2.14.0'


    // event handling
    // using non modularized and same version that guice is using for max compatibility
    // when using latest jpms version here, some classes needed by guice are missing
    implementation('com.google.guava:guava:31.0.1-jre')

    // utils
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
    implementation('org.apache.commons:commons-configuration2:2.10.1')
    implementation('org.apache.commons:commons-lang3:3.12.0')
    // commons configuration 2 depends on this, but does not provide it
    implementation('commons-beanutils:commons-beanutils:1.9.4') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }


    // testing
    testImplementation('org.testfx:testfx-core:4.0.18') {
        exclude module: 'hamcrest-all'
    }
    testImplementation('org.testfx:testfx-junit:4.0.18') {
        exclude module: 'hamcrest-core'
    }
    testImplementation('org.loadui:testFx:3.1.2')
    testImplementation('org.hamcrest:hamcrest-core:1.3')
    testImplementation('junit:junit:4.13.2')
}
